plugins {
    id 'java'
    id 'checkstyle'
    id 'distribution'
    id 'io.quarkus' version '1.7.2.Final' apply false
    id 'com.bmuschko.docker-remote-api' version '5.3.0'
    id 'org.ajoberstar.grgit' version '2.2.1'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.moowork.node' version '1.3.1'
}

ext {
    git = org.ajoberstar.grgit.Grgit.open(currentDir: file('.'))
}

ext['git.revision'] = ext.git.head().id
ext['git.revision.abbrev'] = ext.git.head().abbreviatedId

ext['docker.base.image'] = 'openjdk:8-jre-alpine'
ext['docker.maintainer'] = 'admin@sitewhere.com'

ext['sitewhere.microservice.api.version'] = '3.0.11'
ext['sitewhere.integration.api.version'] = '3.0.2'
ext['kafka.version'] = '2.0.0'
ext['protobuf.version'] = '3.5.1'
ext['grpc.version'] = '1.25.0'
ext['guava.version'] = '27.1-jre'
ext['groovy.version'] = '2.4.12'
ext['siddhi.version'] = '3.1.2'
ext['jackson.version'] = '2.9.10'

allprojects {
    group = 'com.sitewhere'
	version = '3.0.5'

    repositories {
		mavenLocal()
        maven { url "https://repo.maven.apache.org/maven2" }
        maven { url 'http://maven.restlet.org' }
        maven { url "https://jitpack.io" }
        maven { url "http://oss.jfrog.org/artifactory/oss-release-local/" }
        maven { url "http://maven.wso2.org/nexus/content/repositories/releases/" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
}


subprojects {
	license {
		header rootProject.file('HEADER')
    	include "**/*.java"
	}

	// Allow Docker settings to be overridden by build environment.
	if (System.getenv("DOCKER_REGISTRY_URL") != null) registryUrl = System.getenv("DOCKER_REGISTRY_URL")
	if (System.getenv("DOCKER_REGISTRY_USERNAME") != null) registryUsername = System.getenv("DOCKER_REGISTRY_USERNAME")
	if (System.getenv("DOCKER_REGISTRY_PASSWORD") != null) registryPassword = System.getenv("DOCKER_REGISTRY_PASSWORD")
	if (System.getenv("DOCKER_REGISTRY_EMAIL") != null) registryEmail = System.getenv("DOCKER_REGISTRY_EMAIL")

	// Apply Docker plugin.
	apply plugin: 'com.bmuschko.docker-remote-api'
	docker {
	    registryCredentials {
	        url = registryUrl
	        username = registryUsername
	        password = registryPassword
	        email = registryEmail
	    }
	}

	// Apply Quarkus.
	apply plugin: 'io.quarkus'

 	// Choose Java settings.
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

	// Replace tokens with version info.
	processResources {
	    from(sourceSets.main.resources.srcDirs) {
	        include '**/*.properties'
	        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
	            'build.timestamp': new java.text.SimpleDateFormat('yyyyMMddHHmmss').format(new Date()),
	            'version.identifier' : project.version,
	            'git.revision': rootProject.ext['git.revision'],
	            'git.revision.abbrev': rootProject.ext['git.revision.abbrev']
	        ])
	    }
	}
	
	// Create Dockerfile for microservices.
	task dockerFile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile, dependsOn: quarkusBuild) {
    	destFile = project.file('build/docker/Dockerfile')
    	from "ghcr.io/graalvm/graalvm-ce:ol8-java8-20.3.1"
		copyFile("${project.name}-${project.version}-runner.jar", "/")
		copyFile("lib", "/lib")
			
		if(!project.hasProperty("debug")) {
			defaultCommand('java', 
				'-server', 
				'-Xmx512M', 
				'-Xss384K', 
				'-jar', 
				"${project.name}-${project.version}-runner.jar")
		} else {
	    	exposePort 1100
	    	exposePort 8000
	    	defaultCommand('java',
		    	'-server',
		    	'-Xdebug',
		    	'-Dcom.sun.management.jmxremote.local.only=false',
		    	'-Dcom.sun.management.jmxremote.ssl=false',
		    	'-Dcom.sun.management.jmxremote.authenticate=false',
		    	'-Dcom.sun.management.jmxremote.port=1100',
		    	'-Dcom.sun.management.jmxremote.rmi.port=1100',
		    	'-Dcom.sun.management.jmxremote.host=0.0.0.0',
		    	'-Djava.rmi.server.hostname=0.0.0.0',
		    	'-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=8000',
		    	'-Xmx512M',
		    	'-Xss384K',
		    	'-jar',
		    	"${project.name}-${project.version}-runner.jar")	
		}
	}

	// Copy libraries Docker input folder.
	task copyLibsToDocker(type: Copy, dependsOn: dockerFile) {
		into('build/docker/lib')
	    from "${buildDir}/lib"
	}
	
	// Copy runner jar Docker input folder.
	task copyQuarkusToDocker(type: Copy, dependsOn: copyLibsToDocker) {
		into('build/docker')
	    from "${buildDir}/${project.name}-${project.version}-runner.jar"
	}
	
	// Build an image from the Dockerfile.
	task dockerImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage, dependsOn: copyQuarkusToDocker) {
	    inputDir = project.file('build/docker')
		if(!project.hasProperty("debug")) {
		    tags = ["${dockerRepository}/sitewhere/${project.name}:${version}"]
		} else {
		    tags = ["${dockerRepository}/sitewhere/${project.name}:debug-${version}"]		
		}
	}

    // Common dependencies used for all projects.
    dependencies {
		// Confirm to Quarkus BOM.
  		compile platform('io.quarkus:quarkus-bom:1.7.2.Final')

    	// Testing dependencies.
		testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}
