description = 'SiteWhere Batch Management GRPC Model'

apply plugin: 'com.google.protobuf'

dependencies {
    compile project(':sitewhere-grpc-common')
}

protobuf {

	// Configure the protoc executable
	protoc {
	
		// Download from repositories.
		artifact = "com.google.protobuf:protoc:3.3.0"
	}
  
  	// Add GRPC plugin.
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${rootProject.ext['grpc.version']}"
		}
	}
  
	// Choose which tasks are executed.
	generateProtoTasks {
		all().each { task ->
      		task.plugins {
        		grpc {}
      		}
      	}
	}
	
	generatedFilesBaseDir = "$projectDir/src/generated"
}

apply plugin: 'eclipse'
eclipse.classpath.file.whenMerged {
	def grpc = new org.gradle.plugins.ide.eclipse.model.SourceFolder('src/generated/main/grpc', null)
	grpc.getEntryAttributes().put("ignore_optional_problems", "true")
	entries += grpc
	def gjava = new org.gradle.plugins.ide.eclipse.model.SourceFolder('src/generated/main/java', null)
	gjava.getEntryAttributes().put("ignore_optional_problems", "true")
	entries += gjava
}

// Publish jar.
apply plugin: 'maven-publish'
publishing {
	publications {
        mavenJava(MavenPublication) {
            from components.java
        }
	}
}
