import org.ajoberstar.grgit.*

description = 'SiteWhere Web/REST Microservice'

apply plugin: 'org.springframework.boot'
springBoot {
    mainClassName = 'com.sitewhere.web.WebRestApplication'
}

apply plugin: 'com.moowork.node'
node {
	version = '8.10.0'
	npmVersion = '5.7.1'
	nodeModulesDir = file("${projectDir}/sitewhere-ui-build/sitewhere2-admin-ui")
	npmWorkDir = file("${projectDir}/sitewhere-ui-build")
	download = true
}

dependencies {
	compile project(':sitewhere-communication')
	compile project(':sitewhere-microservice')
	compile project(':sitewhere-grpc-client')
	
	compile ('org.springframework.boot:spring-boot-starter-web')
	compile ('org.springframework.boot:spring-boot-starter-websocket')    
	compile group: 'org.springframework', name: 'spring-web'
	
	// Swagger
	compile group: 'io.swagger', name: 'swagger-annotations', version: '1.5.21'
	compile group: 'io.swagger', name: 'swagger-models', version: '1.5.21'
    compile group: 'io.springfox', name: 'springfox-swagger2', version:'2.9.2'
    
    implementation('io.swagger:swagger-annotations:1.5.21') {
        force = true
    }
    implementation('io.swagger:swagger-models:1.5.21') {
        force = true
    }
 
    // Spring Security
    compile group: 'org.springframework.security', name: 'spring-security-config'
    compile group: 'org.springframework.security', name: 'spring-security-web'
	
	// Quartz.
    compile group: 'org.quartz-scheduler', name: 'quartz', version:'2.2.2'
}

// Pull SiteWhere admin user interface from GitHub.
task pullSwaggerUserInterface() {
	// Clone SiteWhere repository.
	delete "${projectDir}/sitewhere-ui-build/sitewhere2-swagger-ui"
	println "Pulling sitewhere2-swagger-ui from GitHub..."
	def repo = Grgit.clone(dir: "${projectDir}/sitewhere-ui-build/sitewhere2-swagger-ui", 
		uri: 'https://github.com/sitewhere/sitewhere2-swagger-ui.git')
	repo.close()
}

// Before building the Spring Boot WAR, copy Swagger interface.
apply plugin: 'war'
bootWar {
	classifier = 'boot'
	dependsOn pullSwaggerUserInterface
	with {
		from( "${projectDir}/sitewhere-ui-build/sitewhere2-swagger-ui" )
	}
}

// Keep original jar.
jar { enabled = true }

// Only publish thin jar.
apply plugin: 'maven-publish'
publishing {
    publications {
	mavenJava(MavenPublication) { from components.java }
    }
}

// Create a Dockerfile.
task dockerFile(type: com.bmuschko.gradle.docker.tasks.image.Dockerfile, dependsOn: bootWar) {
    destFile = project.file('build/docker/Dockerfile')
    from "${rootProject.ext['docker.base.image']}"
    instruction "LABEL maintainer=${rootProject.ext['docker.maintainer']}"
	instruction "RUN wget -qO/bin/grpc_health_probe https://github.com/grpc-ecosystem/grpc-health-probe/releases/download/v0.2.2/grpc_health_probe-linux-amd64 && chmod +x /bin/grpc_health_probe"
	
	// Copy Spring Boot jar.
	copyFile("${project.name}-${project.version}-boot.war", "/")

	// Expose port
	exposePort 8080

	// Run Java command to start application.
	if(!project.hasProperty("debug")) {
		defaultCommand 'java', '-Xmx512M', '-Xss384K', '-jar', "/${project.name}-${project.version}-boot.war"
	} else {
		println "Generating DEBUG IMAGE for project ${project.name}"

		def jdwpPort = 8001
		def jmxPort = 1110
		
		// Set Env 
		environmentVariable('JMX_HOST', '0.0.0.0')

		// Expose ports.
		exposePort jdwpPort
		exposePort jmxPort

		defaultCommand 'java', '-server', '-Xdebug', '-Dcom.sun.management.jmxremote.local.only=false', '-Dcom.sun.management.jmxremote.ssl=false', '-Dcom.sun.management.jmxremote.authenticate=false', "-Dcom.sun.management.jmxremote.port=${jmxPort}", "-Dcom.sun.management.jmxremote.rmi.port=${jmxPort}", '-Dcom.sun.management.jmxremote.host=0.0.0.0', '-Djava.rmi.server.hostname=0.0.0.0', "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=${jdwpPort}", '-Xmx512M', '-Xss384K', '-jar', "/${project.name}-${project.version}-boot.war"
	}
}

// Copy artifact to Docker input folder.
task copyArtifactsToDocker(type: Copy, dependsOn: dockerFile) {
    from "${buildDir}/libs/${project.name}-${project.version}-boot.war"
    into 'build/docker'
}

// Build an image from the Dockerfile.
task dockerImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage, dependsOn: copyArtifactsToDocker) {
    inputDir = project.file('build/docker')
	if(!project.hasProperty("debug")) {
	    tags = ["${dockerRepository}/sitewhere/${project.name}:${version}"]
	} else {
	    tags = ["${dockerRepository}/sitewhere/${project.name}:debug-${version}"]		
	}
}

// Build an image from the Dockerfile with tag latest.
task dockerImageLatest(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage, dependsOn: copyArtifactsToDocker) {
    inputDir = project.file('build/docker')
	
	def primaryTag = ""
	def secondaryTag = ""

	if(!project.hasProperty("debug")) {
		primaryTag = "${dockerRepository}/sitewhere/${project.name}:${version}"
		secondaryTag = "${dockerRepository}/sitewhere/${project.name}:latest"
	} else {
		primaryTag = "${dockerRepository}/sitewhere/${project.name}:debug-${version}"
		secondaryTag = "${dockerRepository}/sitewhere/${project.name}:debug-latest"
	}
	tags = [primaryTag, secondaryTag]
}

// Build an image from the Dockerfile with tag edge.
task dockerImageEdge(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage, dependsOn: copyArtifactsToDocker) {
    inputDir = project.file('build/docker')
	
	def primaryTag = ""
	def secondaryTag = ""

	if(!project.hasProperty("debug")) {
		primaryTag = "${dockerRepository}/sitewhere/${project.name}:${version}"
	} else {
		primaryTag = "${dockerRepository}/sitewhere/${project.name}:debug-${version}"
	}
	secondaryTag = "${dockerRepository}/sitewhere/${project.name}:edge"
	tags = [primaryTag, secondaryTag]
}

// Push image to remote repository.
task dockerPush(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage, dependsOn: dockerImage) {
    imageName = "${dockerRepository}/sitewhere/${project.name}".toString()
    tag = "${version}".toString()
}

// Push image to remote repository.
task dockerPushLatest(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage, dependsOn: dockerImageLatest) {
    imageName = "${dockerRepository}/sitewhere/${project.name}".toString()
}

// Push image to remote repository.
task dockerPushEdge(type: com.bmuschko.gradle.docker.tasks.image.DockerPushImage, dependsOn: dockerImageEdge) {
    imageName = "${dockerRepository}/sitewhere/${project.name}".toString()
}
